#!/usr/bin/env ys-0

defn main():
  show-missing:
  show-by-hand:
  show-versions:

missing =::
- bison
- cabal
- clj-kondo
- cursor
- flex
- fpc
- gcc
- gnat
- graalvm
- java
- lein
- pandoc
- raku

defn show-missing():
  say: 'Check support missing:'
  each name missing:
    url =: read("$name.mk") =~ /https:\/\/\S+/
    printf "* %-10s - %s\n": +"$name.mk" url

parse-re =: qr("(?x)
  [A-Z]+-VERSION\s\?=\s(\d+\..*)\n
  (?s:.*)
  (https://(?:www\.)?github\.com/[-\w]+/[-\w]+)\n
  ")

files =: fs/glob('*.mk'):sort

defn show-versions():
  say:
  say: 'Checking current versions:'

  files =: files
    .remove(\(_:read.re-find(/www\.github/)))
    .remove(\(_.replace(/\.mk/).in?(missing)))

  files =:
    # reduce _ [] ['go.mk']:
    reduce _ [] files:
      fn(files file):
        _ ver url =:
          file:read =~ parse-re
        if ver:
          then:
            phrase =:
              format "* %-13s - %s": file url
            say: phrase
            new =: bash(url:cmd).out:lines:highest
              .replace(/^(?:v|OTP-)/)
              .replace(/(?:\.ROLLING)$/)
            if new != ver:
              conj files: +[file ver new url]
              else: files
          else: files

  each file files:
    say: format('%-20s  %-10s -> %-10s  %s/tags' file*)

defn show-by-hand():
  files =: files
    .filter(\(_:read.re-find(qr("https://www\.github\.com/.*"))))
    .map(\([_
            _:read.re-find(parse-re).1
            _:read.re-find(qr("https://www\.github\.com/.*"))]))

  say: "\nCheck these by hand:"
  each file files:
    say: format("* %-10s - %-11s - %s" file*)

defn cmd(url): |
  git ls-remote \
      --tags \
      $url | \
    cut -d/ -f3 | \
    grep -E '^([a-z]+-?)?[0-9]+\.[0-9]+\.[0-9]+(\.[0-9]+)?$'
#    grep -E '^(v|go)?[0-9]+\.[0-9]+\.[0-9]+(-[-.0-9a-zA-Z]+)?$'

defn highest(list):
  list .=: map(\(_.replace(/^([a-z]+-?)/)))
  last:
    sort-by _ list:
      fn(v):
        reduce _ "" v.split(/[-.]/):
          fn(w s):
            if s =~ /^\d+$/: +
              "$w-$(format('%05d' s:I))"
              "$w-$s"
